import itertools

import pandas as pd

manifest_df = pd.read_table(
    config["manifest"],keep_default_na=False,dtype="str"
).set_index(
    ["sample"],drop=False
)  # sample,fasta,group

target_chr = config.get("target_chr","chrX")
chrom_size = config.get("chrom_size",154259566)  # chm13v2 chrX size
conf_fraction = config.get("confident_fraction",0.80)
svbyeye_cntr = config["svbyeye_cntr"]

# Make sure there is just one entry
assert (
        len(config["reference"].keys()) == 1
), "Only one reference_name:reference_path pair allowed, e.g. chrX_T2T: /path/to/fasta"

ref_name = list(config["reference"].keys()).pop()
ref_path = config["reference"][ref_name]

wildcard_constraints:
    sample="|".join(manifest_df.index),
    hap="|".join(manifest_df.columns),
    chr=target_chr,
    fasta_origin="main|support",
    group="|".join(manifest_df["group"]),
    chrom=target_chr,
    conf_prnct=conf_fraction,
    view="view_\d+",


def get_final_output(wildcards):
    final_output = []

    unique_groups = manifest_df["group"].unique()

    # All vs. all alignment
    for g in unique_groups:
        order = config["view_order"][g]
        visual_order_df = make_pairs(visual_order_string=order)
        unique_idx = visual_order_df.idx.unique()
        final_output.extend(
            [
                f"results/{ref_name}/{target_chr}/confident-mapping_{conf_fraction}/all-vs-all/{g}/{view_idx}/{g}-oriented.pdf"
                for view_idx in unique_idx
            ]
        )

    # Individual files
    final_output.extend(
        expand(
            [
                "results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold.fasta",
                "results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_no-mapping_annotated.bed",
            ],
            ref_name=ref_name,
            conf_prnct=conf_fraction,
            sample=manifest_df.index,
            chrom=target_chr,
        )
    )

    return final_output


def get_fasta(wildcards):
    return manifest_df.at[wildcards.sample, "fasta"]


def make_pairs(visual_order_string):
    # Parse the order string
    pair_fasta_tuple = [
        list(itertools.pairwise(x.split(":"))) for x in visual_order_string.split(";")
    ]

    visual_order_df = pd.DataFrame()
    for idx, x in enumerate(pair_fasta_tuple):
        throwaway_df = pd.DataFrame(data=x,columns=["sample_one", "sample_two"])
        throwaway_df["idx"] = f"view_{idx}"
        visual_order_df = pd.concat([visual_order_df, throwaway_df])

    # pair_fasta_tuple = list(itertools.chain.from_iterable(pair_fasta_tuple))

    visual_order_df.reset_index(drop=True,inplace=True)
    return visual_order_df


def get_group_fasta(wildcards):
    fasta = []

    order = config["view_order"][wildcards.group]

    visual_order_df = make_pairs(visual_order_string=order)

    fp = f"results/{wildcards.ref_name}/{wildcards.chrom}/confident-mapping_{wildcards.conf_prnct}/all-vs-all/{wildcards.group}/{wildcards.view}/{{sample_one}}_vs_{{sample_two}}.paf"

    for row in visual_order_df.query(rf"idx == '{wildcards.view}'").itertuples():
        fasta.append(fp.format(sample_one=row.sample_one,sample_two=row.sample_two))

    return fasta


def get_annotations(wildcards):
    return [v for k, v in config["annotations"].items()]


rule all:
    input:
        get_final_output,


rule make_paf:
    input:
        fa=get_fasta,
        ref=ref_path,
    output:
        paf=temp("results/{ref_name}/confident-mapping_{conf_prnct}/{sample}.paf"),
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "minimap2/2.24",
    params:
        minimap=config.get("minimap_params","-x asm20"),
    threads: 8
    resources:
        mem=12,
        hrs=72,
    shell:
        """
        minimap2 \
            {params.minimap} \
            -t {threads} \
            -o {output.paf} \
            {input.ref} {input.fa}
        """


rule subset_chrom:
    input:
        paf="results/{ref_name}/confident-mapping_{conf_prnct}/{sample}.paf",
    output:
        paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}.paf",
    threads: 1
    resources:
        mem=12,
        hrs=72,
    shell:
        """
        cat {input.paf} | grep -w {wildcards.chrom} > {output.paf}
        """


rule extract_confident_contigs:
    """A confident contig maps 80% of its content to chrX."""
    input:
        paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}.paf",
    output:
        confident_paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping.paf",
        confident_bed="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping.bed",
        confident_contigs=temp(
            "results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping_contigs.txt"
        ),
    run:
        # Please reference the columns here: https://github.com/lh3/miniasm/blob/master/PAF.md
        df = pd.read_table(input.paf,header=None,usecols=range(0,12))

        groupdf = df.groupby([0, 1])[9].sum().reset_index()
        groupdf.loc[:, "fraction"] = groupdf[9] / groupdf[1]
        groupdf = groupdf.query(rf"fraction >= {wildcards.conf_prnct}")

        groupdf.drop(columns=[9],inplace=True)
        df = df.merge(groupdf,how="inner",on=[0, 1]).drop(columns=["fraction"])

        df.to_csv(output.confident_paf,sep="\t",header=False,index=False)

        # contig id: contig-name:start-end
        df.loc[:, 12] = (
                df[0].astype(str) + ":" + df[2].astype(str) + "-" + df[3].astype(str)
        )

        # Since we will output bed-format, the start must be 0-based.
        df[7] = df[7] - 1
        # Group the contig id and sort
        df.groupby([5, 7, 8])[12].apply(",".join).reset_index().to_csv(
            output.confident_bed,sep="\t",header=False,index=False
        )

        # Extract the contig intervals
        # df[12].to_csv(output.confident_contigs_intervals, header=False, index=False)
        # confident_contigs_intervals="results/{ref_name}/{sample}/{fasta_origin}/{asm}_{conf_prnct}_confident-mapping_contig-intervals.txt",

        # Extract the confident contig names
        df[0].drop_duplicates().to_csv(
            output.confident_contigs,header=False,index=False
        )


rule extract_nomaps:
    """Extract regions where there is no contig confidently mapping to chrX"""
    input:
        confident_beds="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping.bed",
    output:
        no_mappings_bed=temp(
            "results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_no-mapping.bed"
        ),
    params:
        chrom_size=chrom_size,
    shell:
        """
        cat {input.confident_beds} \
            | bedtools sort -i - \
            | bedtools merge -i - \
            | bedtools complement -i - -g <(echo -e "{wildcards.chrom}\\t{params.chrom_size}") > {output.no_mappings_bed}
        """


rule annotate_nomaps:
    """Annotate the regions with no mappings to whether they fall in the centromeres or PAR"""
    input:
        annotations=get_annotations,
        no_mappings_bed="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_no-mapping.bed",
    output:
        annotated_bed="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_no-mapping_annotated.bed",
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "bedtools/2.29.2",
    threads: 1
    resources:
        mem=16,
        hrs=72,
    shell:
        """
        bedtools intersect \
            -a {input.no_mappings_bed} \
            -b {input.annotations} \
            -filenames \
            -wb -wa > {output.annotated_bed}
        """


rule contig_fasta:
    """Extract sequences of the confident contig intervals"""
    input:
        contig_names="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping_contigs.txt",
        original_asm=get_fasta,
    output:
        fasta="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping.fasta",
    params:
        target_chr=target_chr,
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "seqtk/1.4",
    threads: 1
    resources:
        mem=16,
        hrs=72,
    shell:
        """
        seqtk subseq \
            {input.original_asm} \
            {input.contig_names} \
            | seqtk seq -Cl60 /dev/stdin > {output.fasta}
        """


rule extract_chrom_ref_fasta:
    input:
        reference_fa=ref_path,
        reference_fa_fai=ref_path + ".fai",
    output:
        chrom_ref_fasta="results/{ref_name}/{chrom}/{chrom}.fasta",
        chrom_ref_fasta_fai="results/{ref_name}/{chrom}/{chrom}.fasta.fai",
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "seqtk/1.4",
    threads: 1
    resources:
        mem=16,
        hrs=72,
    shell:
        """
        seqtk subseq \
            {input.reference_fa} <(echo {wildcards.chrom})  | seqtk seq -Cl60 /dev/stdin > {output.chrom_ref_fasta}

        grep -w {wildcards.chrom} {input.reference_fa_fai} > {output.chrom_ref_fasta_fai}
        """


rule ragtag_scaffold:
    input:
        query_fasta="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/{sample}_confident-mapping.fasta",
        ref_fasta="results/{ref_name}/{chrom}/{chrom}.fasta",
        ref_fai="results/{ref_name}/{chrom}/{chrom}.fasta.fai",
    output:
        ragtag_scaffold_fasta="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold.fasta",
        ragtag_scaffold_fasta_fai="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold.fasta.fai",
        ragtag_scaffold_stats="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold.stats",
        ragtag_scaffold_agp="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold.agp",
        ragtag_scaffold_subset_fasta="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold_subset.fasta",
        ragtag_scaffold_subset_fai="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/{sample}/ragtag/scaffold/ragtag.scaffold_subset.fasta.fai",
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "ragtag/2.1.0",
        "samtools/1.14",
    threads: 16
    resources:
        mem=8,
        hrs=72,
    shell:
        """
        ragtag.py scaffold \
            --mm2-params "-x asm20" \
            -u -t {threads} \
            -o $(dirname {output.ragtag_scaffold_fasta}) \
             {input.ref_fasta} {input.query_fasta} \
         && \
            sed -ri "s/(>.*)/\\1-{wildcards.sample}/" {output.ragtag_scaffold_fasta} && samtools faidx {output.ragtag_scaffold_fasta}

        wait

        grep "^{wildcards.chrom}" {output.ragtag_scaffold_fasta_fai} > {output.ragtag_scaffold_subset_fai}
        samtools faidx {output.ragtag_scaffold_fasta} -r <( cut -f1 {output.ragtag_scaffold_subset_fai} ) > {output.ragtag_scaffold_subset_fasta}
        """


def get_desired_pair(wildcards):
    if wildcards.sample_one != ref_name:
        desired_paired_ref = f"results/{ref_name}/{wildcards.chrom}/confident-mapping_{wildcards.conf_prnct}/{wildcards.sample_one}/ragtag/scaffold/ragtag.scaffold_subset.fasta"
    else:
        desired_paired_ref = (
            f"results/{wildcards.ref_name}/{wildcards.chrom}/{wildcards.chrom}.fasta"
        )

    if wildcards.sample_two != ref_name:
        desired_paired_query = f"results/{ref_name}/{wildcards.chrom}/confident-mapping_{wildcards.conf_prnct}/{wildcards.sample_two}/ragtag/scaffold/ragtag.scaffold_subset.fasta"
    else:
        desired_paired_query = (
            f"results/{wildcards.ref_name}/{wildcards.chrom}/{wildcards.chrom}.fasta"
        )

    return {"ref": desired_paired_ref, "query": desired_paired_query}


rule aln_again:
    input:
        unpack(get_desired_pair),
    output:
        paf=temp(
            "results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/all-vs-all/{group}/{view}/{sample_one}_vs_{sample_two}.paf"
        ),
    params:
        minimap=config.get("minimap_params","-x asm20"),
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "minimap2/2.24",
    threads: 16
    resources:
        mem=8,
        hrs=72,
    shell:
        """
        minimap2 \
            {params.minimap} \
            -c \
            -t {threads} \
            -o {output.paf} \
            {input.ref} {input.query}
        """


rule ava_aln:
    input:
        group_fasta=get_group_fasta,
    output:
        group_paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/all-vs-all/{group}/{view}/{group}.paf",
    shell:
        """
        cat {input.group_fasta} > {output.group_paf}
        """


rule orient_majority:
    input:
        ava_paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/all-vs-all/{group}/{view}/{group}.paf",
    output:
        oriented_ava_paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/all-vs-all/{group}/{view}/{group}-oriented.paf",
    envmodules:
        "modules",
        "modules-init",
        "modules-gs/prod",
        "modules-eichler/prod",
        "rustybam/0.1.33",
    threads: 1
    resources:
        mem=4,
        hrs=72,
    shell:
        """
        rustybam orient {input.ava_paf} > {output.oriented_ava_paf}

        # Need to adjust first column of output because rustybam appends the strand to the first column string.
        sed -i -r 's/([^\\t])([+-]{{1}})\\t(.*)/\\1\\t\\3/' {output.oriented_ava_paf}
        """


rule svbyeye:
    input:
        oriented_ava_paf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/all-vs-all/{group}/{view}/{group}-oriented.paf",
    output:
        ava_pdf="results/{ref_name}/{chrom}/confident-mapping_{conf_prnct}/all-vs-all/{group}/{view}/{group}-oriented.pdf",
    container:
        svbyeye_cntr
    threads: 1
    resources:
        mem=4,
        hrs=72,
    script:
        "scripts/plot-ava.R"
